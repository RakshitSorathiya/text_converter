/*
 * Copyright (C)  2017-2018 Tran Le Duy
 *
 *  This program is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation, either version 3 of the License, or
 *  (at your option) any later version.
 *
 *  This program is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 *  You should have received a copy of the GNU General Public License
 *  along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 401
        versionName "4.0.1"

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    signingConfigs {
        proRelease {

        }
        freeRelease {

        }
    }
    flavorDimensions "applicationId"
    productFlavors {
        free {
            signingConfig signingConfigs.freeRelease
            applicationId "duy.com.text_converter"
        }
        pro {
            signingConfig signingConfigs.proRelease
            applicationId "com.duy.text_converter.pro"
        }
    }

    sourceSets {
        main.res.srcDirs += 'src/main/res-cipher'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
        }
    }
}

def Properties props = new Properties()
def propFile = new File(rootProject.projectDir, 'signing.properties')
if (propFile.exists()) {
    props.load(new FileInputStream(propFile))
    android.signingConfigs.proRelease.storeFile = file(props['PRO_RELEASE_STORE_FILE'])
    android.signingConfigs.proRelease.storePassword = props['PRO_RELEASE_STORE_PASSWORD']
    android.signingConfigs.proRelease.keyAlias = props['PRO_RELEASE_KEY_ALIAS']
    android.signingConfigs.proRelease.keyPassword = props['PRO_RELEASE_KEY_PASSWORD']

    android.signingConfigs.freeRelease.storeFile = file(props['FREE_RELEASE_STORE_FILE'])
    android.signingConfigs.freeRelease.storePassword = props['FREE_RELEASE_STORE_PASSWORD']
    android.signingConfigs.freeRelease.keyAlias = props['FREE_RELEASE_KEY_ALIAS']
    android.signingConfigs.freeRelease.keyPassword = props['FREE_RELEASE_KEY_PASSWORD']
} else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    testImplementation 'junit:junit:4.12'
    implementation project(':lvl')
    implementation project(':common')

    implementation "com.android.support:appcompat-v7:$androidSupportVersion"
    implementation "com.android.support:cardview-v7:$androidSupportVersion"
    implementation "com.android.support:design:$androidSupportVersion"
    implementation "com.android.support:support-v4:$androidSupportVersion"

    implementation group: 'commons-lang', name: 'commons-lang', version: '2.6'
    implementation 'com.google.zxing:core:3.3.0'
    implementation 'com.journeyapps:zxing-android-embedded:3.5.0@aar'
    implementation 'com.ogaclejapan.smarttablayout:library:1.6.1@aar'
    implementation 'com.simplecityapps:recyclerview-fastscroll:1.0.16'
    implementation 'io.github.kobakei:ratethisapp:1.2.0'
    implementation 'com.github.PhilJay:MPAndroidChart:v2.2.4'
}
apply plugin: 'com.google.gms.google-services'